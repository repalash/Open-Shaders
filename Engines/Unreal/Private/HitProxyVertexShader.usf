// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	HitProxyVertexShader.hlsl: Vertex shader for rendering hit proxies.
=============================================================================*/

// Some input nodes can't compute their output value at hit proxy rendering time, and so their implementation changes.
#define HIT_PROXY_SHADER 1

#include "Common.ush"
#include "/Engine/Generated/Material.ush"
#include "/Engine/Generated/VertexFactory.ush"

// Whether HitProxy IDs are provided per instance or not
#define PER_INSTANCE_HITPROXY_ID	(USE_INSTANCING || USE_PER_VERTEX_HITPROXY_ID || USE_VERTEXFACTORY_HITPROXY_ID)

#if USE_PER_VERTEX_HITPROXY_ID
Buffer<float4> VertexFetch_HitProxyIdBuffer;
#endif

struct FHitProxyVSToPS
{
	FVertexFactoryInterpolantsVSToPS FactoryInterpolants;
#if PER_INSTANCE_HITPROXY_ID
	float4 InstanceHitProxyId : HIT_PROXY_ID;
#endif
	float4 PixelPosition : TEXCOORD6;
#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
	float3 PixelPositionExcludingWPO : TEXCOORD7;
#endif
	float4 Position : SV_POSITION;
};

#if USING_TESSELLATION	
	struct FHitProxyVSToDS
	{
		FVertexFactoryInterpolantsVSToDS FactoryInterpolants;
	    #if PER_INSTANCE_HITPROXY_ID
		    float4 InstanceHitProxyId : HIT_PROXY_ID;
	    #endif
		float4 Position : VS_To_DS_Position;
		#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
			float3 PixelPositionExcludingWPO : TEXCOORD7;
		#endif
		OPTIONAL_VertexID_VS_To_DS
	};
	
	#define FHitProxyVSOutput FHitProxyVSToDS
#else
	#define FHitProxyVSOutput FHitProxyVSToPS
#endif

#if USING_TESSELLATION	
	#define FPassSpecificVSToDS FHitProxyVSToDS
	#define FPassSpecificVSToPS FHitProxyVSToPS
	
	FHitProxyVSToDS PassInterpolate(FHitProxyVSToDS a, float aInterp, FHitProxyVSToDS b, float bInterp)
	{
		FHitProxyVSToDS O;

		O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);

		#if PER_INSTANCE_HITPROXY_ID
			TESSELLATION_INTERPOLATE_MEMBER(InstanceHitProxyId);
		#endif

		#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
			TESSELLATION_INTERPOLATE_MEMBER(PixelPositionExcludingWPO);
		#endif

		return O;
	}

	FHitProxyVSToPS PassFinalizeTessellationOutput(FHitProxyVSToDS Interpolants, float4 WorldPosition, FMaterialTessellationParameters MaterialParameters)
	{
		FHitProxyVSToPS O;
		
		O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);
		
		#if PER_INSTANCE_HITPROXY_ID
			O.InstanceHitProxyId = Interpolants.InstanceHitProxyId;
		#endif

		// Finally, transform position to clip-space
		{
			O.Position = mul(WorldPosition, View.TranslatedWorldToClip);
		}
			
		O.PixelPosition = WorldPosition;

		#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
			O.PixelPositionExcludingWPO = Interpolants.PixelPositionExcludingWPO.xyz;
		#endif
		
		return O;
	}
	
	#include "Tessellation.ush"
#endif

#if VERTEXSHADER

void Main(
	FVertexFactoryInput Input,
	OPTIONAL_VertexID
	out FHitProxyVSOutput Output
#if USE_GLOBAL_CLIP_PLANE && !USING_TESSELLATION
	, out float OutGlobalClipPlaneDistance : SV_ClipDistance
#endif
	)
{
	ResolvedView = ResolveView();

	FVertexFactoryIntermediates VFIntermediates = GetVertexFactoryIntermediates(Input);
	float4 WorldPosition = VertexFactoryGetWorldPosition(Input, VFIntermediates);
	float4 WorldPositionExcludingWPO = WorldPosition;
	float3x3 TangentToLocal = VertexFactoryGetTangentToLocal(Input, VFIntermediates);

	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPosition.xyz, TangentToLocal);
	WorldPosition.xyz += GetMaterialWorldPositionOffset(VertexParameters);

#if USE_PER_VERTEX_HITPROXY_ID
	#if MANUAL_VERTEX_FETCH
		Output.InstanceHitProxyId = VertexFetch_HitProxyIdBuffer[(VF_VertexOffset + Input.VertexId)] FMANUALFETCH_COLOR_COMPONENT_SWIZZLE;
	#else
		Output.InstanceHitProxyId = float4(0,0,0,0);
	#endif
#elif USE_INSTANCING || USE_VERTEXFACTORY_HITPROXY_ID
	Output.InstanceHitProxyId = VertexFactoryGetInstanceHitProxyId(Input, VFIntermediates);
#endif

#if USING_TESSELLATION
	Output.Position = WorldPosition;
	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToDS(Input, VFIntermediates, VertexParameters);
#else

	{
		float4 RasterizedWorldPosition = VertexFactoryGetRasterizedWorldPosition(Input, VFIntermediates, WorldPosition);
		Output.Position = mul(RasterizedWorldPosition, View.TranslatedWorldToClip);
	}

#if USE_GLOBAL_CLIP_PLANE
	OutGlobalClipPlaneDistance = dot(ResolvedView.GlobalClippingPlane, float4(WorldPosition.xyz - ResolvedView.PreViewTranslation.xyz, 1));
#endif

	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToPS(Input, VFIntermediates, VertexParameters);
	Output.PixelPosition = WorldPosition;
#endif

#if USE_WORLD_POSITION_EXCLUDING_SHADER_OFFSETS
	Output.PixelPositionExcludingWPO = WorldPositionExcludingWPO.xyz;
#endif

	OutputVertexID( Output );
}

#endif // VERTEXSHADER
